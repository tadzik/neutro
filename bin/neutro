#!/usr/bin/env perl6
use v6;
use File::Mkdir;
use File::Copy;
use Module::Build;
use Module::Test;
use Module::Install;

my $home = ($*VM<config><osname> eq 'MSWin32')
            ?? %*ENV<HOMEPATH> !! %*ENV<HOME>;
my $CONFIGDIR = "$home/.neutro";
my $INSTALLDIR = %*ENV<DESTDIR> // "$home/.perl6";
my $INSTALLED = "$CONFIGDIR/installed";
my $SRCDIR = "$CONFIGDIR/src";
my %modules;

sub indir (Str $where, Callable $what) {
    my $old = cwd;
    mkdir $where, :p;
    chdir $where;
    $what();
    chdir $old;
}
# check if modules list is present, update it otherwise
sub checklist {
    unless $CONFIGDIR.IO ~~ :d {
        mkdir $CONFIGDIR, :p;
    }
    unless "$CONFIGDIR/projects.list".IO ~~ :f {
        updatedb;
    }
    unless $INSTALLED.IO ~~ :f {
        open($INSTALLED, :w).close;
    }
    my $fh = open "$CONFIGDIR/projects.list";
    for $fh.lines -> $line {
        next if $line ~~ /^\#/;
        my ($name, $url) = $line.split: ' ';
        %modules{$name} = $url;
    }
    $fh.close;
}

sub crap (Str $msg) {
    notice $msg;
    exit 1;
}

sub fetch (Str $name, Str $url) {
    my $res;
    indir $SRCDIR, {
        notice "Fetching $name";
        if "$SRCDIR/$name".IO ~~ :d {
            indir $name, {
                $res = run 'git pull -q';
                crap "Failed updating the $name repo" if $res;
            };
        } else {
            $res = run "git clone -q $url $name";
            crap "Failed cloning the $name repo" if $res;
        }
    };
}

sub install (Str $module, Bool $strict, Bool $v, Bool :$fetch = True) {
    my $res;
    if $fetch {
        %modules.exists($module) or crap "Unknown module $module";
        fetch $module, %modules{$module};
    }
    installdeps $module, $strict, $v;

    notice "Building $module";
    my $fail = False;
    try {
        Module::Build::build :dir("$SRCDIR/$module"), :$v;
        CATCH {
            $fail = True;
        }
    }
    crap "Building $module failed" if $fail;

    # THINKABOUT: what if tests are someplace else?
    # Everyone's allowed to do it
    if $strict and 't'.IO !~~ :d {
        crap "No tests for $module";
    }
    notice "Testing $module";
    try {
        Module::Test::test "$SRCDIR/$module", :$v;
        CATCH {
            $fail = True;
        }
    }
    crap "Tests failed for $module" if $fail;

    notice "Installing $module";
    try {
        Module::Install::install "$SRCDIR/$module", :$v;
        CATCH {
            $fail = True;
        }
    }
    crap "Installing $module failed" if $fail;

    notice "Successfully installed $module";

    unless isinstalled $module {
        given open($INSTALLED, :a) {
            .say($module);
            .close;
        }
    }
}

sub installdeps (Str $name, $strict, $v) {
    indir "$SRCDIR/$name", {
        if 'deps.proto'.IO ~~ :f {
            my $fh = open 'deps.proto';
            for $fh.lines -> $dep {
                next if $dep ~~ /^\#/;
                next if $dep eq '';
                unless isinstalled $dep {
                    notice "verbose" if $v;
                    install $dep, $strict, $v;
                }
            }
            $fh.close;
        }
    };
}

sub isinstalled (Str $module) {
    my $isit;
    given open($INSTALLED) {
        $isit = .lines.first($module).Bool;
        .close;
    }
    return $isit;
}

sub listmodules {
    for %modules.keys.sort.grep({ $_ }) { .say }
}

sub notice (Str $what) {
    say "==> $what";
}

sub updatedb {
    notice 'Updating modules database';
    indir $CONFIGDIR, {
        unlink 'projects.list' if 'projects.list'.IO ~~ :e;
        fetch 'ecosystem', 'git://github.com/perl6/ecosystem.git';
        cp "$SRCDIR/ecosystem/projects.list", "$CONFIGDIR/projects.list";
    };
}

multi MAIN ('list') {
    checklist;
    listmodules;
}

multi MAIN ('update') {
    updatedb;
}

multi MAIN ($module, Bool :$strict, Bool :$v = False) {
    checklist;
    install $module, $strict, $v, :fetch($module eq '.' ?? False !! True);
}

# vim: ft=perl6
